group 'com.bennyhuo'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName="com.bennyhuo.qcloud.MainKt"
applicationDefaultJvmArgs = ['-Dapp.home=MY_APP_HOME']

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
    }
}

distributions {
    main {
        contents {
            from("src/config") {
                into "config"
            }
        }
    }
}

sourceCompatibility = 1.8

repositories {
    maven {
        url "http://maven.aliyun.com/nexus/content/groups/public/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile ("com.qcloud:cos_api:4.4"){
        exclude(group: "org.slf4j")
    }

    compile 'commons-cli:commons-cli:1.2'
    compile 'com.google.code.gson:gson:2.6.1'

    compile "ch.qos.logback:logback-classic:1.2.3"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}